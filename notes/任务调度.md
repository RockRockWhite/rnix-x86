# 操作系统任务调度

## 内存分页

当前我们内存使用分页, 一个page有4k。长度0x1000。

以0x1000开始的这个page为例子。

这个区域作为栈来使用，由于栈是方向增长，所以，栈顶为0x2000, 0x1000为栈底。

## 进程控制块PCB

在栈底0x1000的位置存放一个进程控制块，目前进程控制块存放的内容很简单

```rust
pub struct Task {
    pub stack: *mut TaskFrame, // 任务栈信息
}
```

PCB里就存放了进程切换的时候的任务栈信息，其实就是进城切换时的esp寄存器的值。

## 进程切换的实现

首先定一个数据结构，这个数据结构里能保存恢复一个进程执行的基本信息。目前就这么多了。

```rust
pub struct TaskFrame {
    pub edi: usize,
    pub esi: usize,
    pub ebx: usize,
    pub ebp: usize,
    pub eip: usize,
}
```

主要来看看核心的部分，进程切换，此处我使用汇编实现。

```assembly
task_switch:
    push ebp
    mov ebp, esp

    push ebx
    push esi
    push edi

    mov eax, esp
    and eax, 0xFFFFF000  ; Task变量的地址

    mov [eax], esp ; 保存当前任务栈信息

    mov eax, [ebp + 8] ; 读next参数
    mov esp, [eax] ; 切换任务栈

    pop edi
    pop esi
    pop ebx
    pop ebp

    ret
```

在调用task_switch函数的时候，call指令本身就会在栈中压入调用后位置eip的值，其实也就是等会切换回这个进程的时候要返回的位置的地址。

```assembly
call task_switch
```

```mermaid
graph TB
	B(eip) --> A(...)
```



开头两行，是栈帧的处理, 此处回保存进程切换前的ebp

```assembly
push ebp
mov ebp, esp
```

```mermaid
graph TB
	C(ebp) --> B(eip) --> A(...)
```

紧接着，就是按照TaskFrame这个结构体描述的数据结构，把相关的字段压入栈中。

```assembly
    push ebx
    push esi
    push edi
```

```mermaid
graph TB
F(edi) -->	E(esi) --> D(ebx) --> C(ebp) --> B(eip) --> A(...)
```

紧接着，我们读取当前的任务的进程的PCB地址(即当前所在栈的基地址)，并且把此时的esp寄存器存入，也就是把切换前的栈信息保存入PCB。

```assembly
mov eax, esp
and eax, 0xFFFFF000  ; Task变量的地址

mov [eax], esp ; 保存当前任务栈信息
```

到此为止，我们就完成了切换前的任务信息的保存

---

接着，就需要开始切换任务了。

```assembly
    mov eax, [ebp + 8] ; 读next参数
    mov esp, [eax] ; 切换任务栈

    pop edi
    pop esi
    pop ebx
    pop ebp
    
    ret
```

有了上面的铺垫，切换任务就很好理解了。

ebp + 8处是传入的需要调度的任务的PCB地址，我们从此处读取出需要还原的栈的esp，此时栈就还原到了切换前。

我们再按照保存的顺序，以此还原寄存器。

还原完后栈的状态：

```mermaid
graph TB
B(eip) --> A(...)
```

接着，ret函数将会读取栈顶的值，也就是要回去的指令地址，并切换为该位置。